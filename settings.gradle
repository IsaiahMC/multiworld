pluginManagement {
    repositories {
        maven {
            name = 'Fabric'
            url = 'https://maven.fabricmc.net/'
        }
        maven { url "https://maven.architectury.dev/" }
        maven { url "https://files.minecraftforge.net/maven/" }
		maven {
            name = 'Jitpack'
            url = 'https://jitpack.io'
        }
        gradlePluginPortal()
    }
	resolutionStrategy {
        eachPlugin {
            if (requested.id.id.equals("com.replaymod.preprocess")) {
                useModule("com.github.replaymod:preprocessor:${requested.version}")
            }
        }
    }
}

rootProject.name = 'Multiworld'

include 'Multiworld-Common'

// Fabric
// include 'Multiworld-Fabric-1.18.2'
// include 'Multiworld-Fabric-1.19.2'
// include 'Multiworld-Fabric-1.19.4'

// NeoForged
// include 'Multiworld-NeoForged-1.21.1'
// include 'Multiworld-NeoForged-1.21.2'

// Mod Platform Versions
def versions_fabric   = ['1.20.1', '1.20.4', '1.20.6', '1.21.1', '1.21.4', '1.21.5', '1.21.10', '1.21.11'] // TODO: Should keep 1.18/1.19?
def versions_forge    = ['1.20.1'] // ['1.18.2', '1.19.4', '1.20.1']
def versions_neoforge = ['1.21.1', '1.21.2', '1.21.10']

// Include Platform Projects
versions_fabric.each { projectName ->
	def wholePath = 'Multiworld-Fabric-' + projectName
	include(':' + wholePath)
	project(':' + wholePath).projectDir = new File(rootDir, 'fabric/' + wholePath)
}

include 'Multiworld-Fabric-bundle'

versions_neoforge.each { projectName ->
	def wholePath = 'Multiworld-NeoForged-' + projectName
	include(':' + wholePath)
	project(':' + wholePath).projectDir = new File(rootDir, 'neoforge/' + wholePath)
}

// Forge
// include 'Multiworld-Forge-1.18.2'
// include 'Multiworld-Forge-1.19.4'
include 'Multiworld-Forge-1.20.1'
